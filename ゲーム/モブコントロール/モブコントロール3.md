以下を参考に3Dゲームを作成する。
一通りの知識を学べる。

## 【自分の記事まとめ】ゲームエンジンGodot4.0でスマホ3Dゲームを作ってみる
https://qiita.com/FootInGlow/items/5754cee8f4c691cbc170

---

## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その９　敵を検知するとホーミングするようにしたい
https://qiita.com/FootInGlow/items/5b1c78669e2d56d98f1b

### 敵（円柱）を配置する

円柱にぶつかっても表面を滑って弾が通り過ぎてゆく。
![[Pasted image 20250205013640.png]]
### Collisionレイヤーを定義する

衝突・検知のノードのグループを定義する。
プロジェクトメニューのプロジェクト設定でLayer Names/3D物理を開き、レイヤーを設定。

![[Pasted image 20250205013900.png]]


各ノードにレイヤーを設定する。

| Layerに設定するもの      | Maskに設定するもの（衝突・検出したいもの）                                             |
| ----------------- | ------------------------------------------------------------------- |
| FloorAndObstacles | なし（検出されるが、自ら衝突/検出したいということはない）                                       |
| Player            | FloorAndObstacles（そのうち左右に壁を作って移動制限をしたい）                             |
| Enemy             | なし（今のところ移動予定はない）                                                    |
| Bullet            | FloorAndObstacles、Enemy、Bulletに衝突してほしいが検出はしない（Bullet同士も重ならないようにしたい） |
|                   |                                                                     |
以下のように、シーンで対象のノードを選択して、インスペクタで設定。
![[Pasted image 20250205014333.png]]

| Node              | Layer（自分の属性）        | Mask（衝突を検知したい対象）                     |
| ----------------- | ------------------- | ------------------------------------ |
| FloorAndObstacles | 1：FloorAndObstacles | なし                                   |
| Player            | 2：Player            | 1：FloorAndObstacles                  |
| Enemy             | 3：Enemy             | なし                                   |
| Bullet            | 4：Bullet            | 1：FloorAndObstacles、3：Enemy、4：Bullet |
|                   |                     |                                      |

ある程度近づいたら敵に向かうようにしたい。
### Enemyに円を設定する

敵にArea3Dを設定。形は球体とし、Enemyを包むように。

![[Pasted image 20250205015535.png]]
![[Pasted image 20250205015726.png]]

![[Pasted image 20250205015853.png]]


| Node              | Layer（自分の属性）        | Mask（衝突を検知したい対象）                     |
| ----------------- | ------------------- | ------------------------------------ |
| FloorAndObstacles | 1：FloorAndObstacles | なし                                   |
| Player            | 2：Player            | 1：FloorAndObstacles                  |
| Enemy             | 3：Enemy             | なし                                   |
| Bullet            | 4：Bullet            | 1：FloorAndObstacles、3：Enemy、4：Bullet |
| BulletSensor      | なし                  | 4：Bullet                             |

Enemyにスクリプトをアタッチし、BulletSensorからシグナルを設定。`PhysicsBody3D`ノードを検出したいので、`body_entered(body:Node3D)`を右クリックして接続。接続先はEnemyのスクリプト。

![[Pasted image 20250205020126.png]]

![[Pasted image 20250205020246.png]]
CharacterBody3DやRigidBody3Dなど検出したい場合は、body_xxx シグナル。
Area3Dを検出したい場合は、area_xxx シグナルを使用する。

Enemyスクリプト
```python
extends CharacterBody3D

func _on_bullet_sensor_body_entered(body):
	print(body)
```

BulletがBulletSensorの円の中にはいると、コンソールにノード名（Bullet）がprintされるのが分かる。

![[Pasted image 20250205021340.png]]

### Enemyの位置をBulletに通知する

Enemyスクリプト
```python
extends CharacterBody3D

func _on_bullet_sensor_body_entered(body):
	body.discover_enemy(transform.origin)
```

Bulletスクリプト
```python
extends CharacterBody3D

@export var m_v_dir = Vector3(0.0, 0.0, -1.0)
@export var m_d_speed_mps = 2.0

var m_v3_enemy_pos = null

func discover_enemy(enemy_pos):
	m_v3_enemy_pos = enemy_pos

func _physics_process(delta):
	if m_v3_enemy_pos:
		m_v_dir = m_v3_enemy_pos - transform.origin
	else:
		m_v_dir = Vector3.FORWARD
	velocity = m_v_dir.normalized() * m_d_speed_mps
	move_and_slide()

func _on_visible_on_screen_notifier_3d_screen_exited() -> void:
	queue_free()

```

メンバ変数初期値のVector3(0, 0, -1)をVector3.FORWARDに修正
- @export var m_v_dir = Vector3.FORWARD
	Vector3に定義されている定数に変更。値としてはVector3(0, 0, -1)なので同じ。

func _physics_process(delta):の修正
- if m_v3_enemy_pos:  
    　m_v3_enemy_posが設定されている場合
- m_v_dir = m_v3_enemy_pos - transform.origin  
    　目標位置から現在位置を引くと、現在位置から目標位置へ向かう方向になる
- else:  
    　m_v3_enemy_posが設定されていない場合
- m_v_dir = Vector3.FORWARD  
    　今まで通り前進する（Z軸マイナス方向）

BulletがEnemyに近づくと、Enemyに向きを変えて、最終的にくっつくようになった。
![[Pasted image 20250205022734.png]]

---
## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その１０　敵を増やす、Bullet vs Enemyの処理を実装
https://qiita.com/FootInGlow/items/5b1c78669e2d56d98f1b

敵をシーンファイル化して、複数設定できるようにする。
（Unityのプレハブ化みたいなものか）

BulletがEnemyに当たった場合、お互い攻撃してHPが０になったら消える様にする。

### Enemyブランチをシーンファイルにする

![[Pasted image 20250206002057.png]]
シーン化した。
![[Pasted image 20250206002245.png]]

### Enemyを追加する

rootを右クリックして、「子シーンをインスタンス化」を実行。
先程シーン化した「enemy.tscn」を選ぶ。

![[Pasted image 20250206002450.png]]

### Enemy、Bulletに攻撃力とHPを設定する

`enemy.gd`に追加
```python
@export var m_attack_power_ps = 1.0
@export var m_hp = 10
```

`bullet.gd`に追加
```python
@export var m_attack_power_ps = 1.0
@export var m_hp = 1
```

### Enemyを「EnemyGroup」グループに設定

Bulletが衝突したノードが攻撃対象であるか判定を簡単にするため、グループ機能を利用する。
グループに登録したノードは検索できるようになる。

`enemy.tscn`を開いてEnemyを選択。
インスペクタの横のノード/グループを選択。
「EnemyGroup」と入力して、追加ボタンを押下。

![[Pasted image 20250206003806.png]]
シーングループとして「EnemyGroup」を追加。

![[Pasted image 20250206003941.png]]

### Bulletに攻撃処理を追加する

`bullet.gd`の`_physics_process()`の`move_and_slide()`実行後に攻撃処理を追加。

```python
extends CharacterBody3D

@export var m_v_dir = Vector3(0.0, 0.0, -1.0)
@export var m_d_speed_mps = 2.0
@export var m_attack_power_ps = 1.0
@export var m_hp = 1

var m_v3_enemy_pos = null

func discover_enemy(enemy_pos):
	m_v3_enemy_pos = enemy_pos

func _physics_process(delta):
	if m_v3_enemy_pos:
		m_v_dir = m_v3_enemy_pos - transform.origin
	else:
		m_v_dir = Vector3.FORWARD
	velocity = m_v_dir.normalized() * m_d_speed_mps
	move_and_slide()

	# 攻撃判定
	for index in range(get_slide_collision_count()):
		var collision = get_slide_collision(index)
		if (collision.get_collider() == null):
			continue
		if collision.get_collider().is_in_group("EnemyGroup"):
			var enemy = collision.get_collider()
			
			# Bulletの攻撃
			enemy.m_hp -= m_attack_power_ps * delta
			# Enemyの攻撃
			m_hp -= enemy.m_attack_power_ps * delta

	# HPが0になったら消える
	if m_hp < 0:
		queue_free()

func _on_visible_on_screen_notifier_3d_screen_exited() -> void:
	queue_free()
```

衝突情報をチェックして、Enemyを検索

- `for index in range(get_slide_collision_count()):`  
    `get_slide_collision_count()`により、`move_and_slide()`の結果、衝突した回数（衝突により方向を変えた回数）を取得し、forループする。
- `var collision = get_slide_collision(index)`  
    　index番目の衝突情報をcollisionとして取得する。
- `if (collision.get_collider() == null):`  
    　collision衝突情報がない場合continueして次を検索する。
- `if collision.get_collider().is_in_group("EnemyGroup"):`  
    　衝突したノードが「EnemyGroup」に属しているかチェックする。
- `var enemy = collision.get_collider()`  
    　「EnemyGroup」グループに所属しているノードインスタンスを取得する。

Enemyと接触している場合、お互いの攻撃力を使用してHPを削る。

1秒間でHPを削る量なので、deltaを掛けた攻撃量をHPから減算する。（雑）

- `enemy.m_hp -= m_attack_power_ps * delta`
- `m_hp -= enemy.m_attack_power_ps * delta`

ループを抜けた後、Bulletのhpが0になっていたらBulletを消す。

- `if m_hp < 0:`
- `queue_free()`

### EnemyのHPチェック処理を追加

enemy.gdを開いてhpをチェックする処理を追加。
EnemyのHPが0になったら、Enemyを削除する。
`_physics_process()`が追加した処理。

```python
extends CharacterBody3D

@export var m_attack_power_ps = 1.0
@export var m_hp = 10

func _physics_process(delta):
	if m_hp < 0.0:
		queue_free()
	
func _on_bullet_sensor_body_entered(body):
	body.discover_enemy(transform.origin)
```