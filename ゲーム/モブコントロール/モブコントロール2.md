以下を参考に3Dゲームを作成する。
一通りの知識を学べる。

## 【自分の記事まとめ】ゲームエンジンGodot4.0でスマホ3Dゲームを作ってみる
https://qiita.com/FootInGlow/items/5754cee8f4c691cbc170

---

## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その５　Godotエンジンによる衝突の制御
https://qiita.com/FootInGlow/items/a55b1137fc2a89b7aa20

|ノード|用途|
|---|---|
|StaticBody3D|動かないので、主に地形、壁、建物、固定オブジェクトの表現に適しています。|
|CharacterBody3D|移動やジャンプなどの自由な操作を実装することができますので、主にキャラクターやプレイヤーに適しています。|
|RigidBody3D|物理エンジンを使用した3Dオブジェクトのシミュレーションにより動きます。|
|Area3D|衝突せず通過しますが、他ノードの衝突（エリアに入る／出る）を検出することができます。|

### 衝突形状
衝突する形状はCollisionShape3Dに設定。

### RigidBody3D

RigidBody3Dで球体を設置。
実行すると落ちてゆく。

![[Pasted image 20250131001757.png]]

### StaticBody3D

StaticBody3Dで板を設置。
実行すると宙に浮く。

![[Pasted image 20250131002137.png]]

落下した球体は板の上で止まる。
![[Pasted image 20250131002252.png]]

板を傾けると転がる。
![[Pasted image 20250131002408.png]]

ボールを跳ねるようにする。
RigidBody3Dを選択して、インスペクタ内のRigidBody3D/Physics Materialのをクリックして新規PhysicsMaterialをクリック。
PhysicsMaterialをクリックするとBounceという項目があるので、0.5に変更。

![[Pasted image 20250131002803.png]]

ボールがバウンドしてから落ちてゆく。
![[Pasted image 20250131002839.png]]

### CharacterBody3D

CharacterBody3Dとして追加した立方体は空中で止まる。
動かすにはGDScriptで実装する必要あり。

![[Pasted image 20250131003302.png]]

テンプレート：CharacterBody3D:Basic Movementを指定して
スクリプトをアタッチする。

![[Pasted image 20250131003415.png]]
スペースでジャンプ、カーソルで移動するようになる。
![[Pasted image 20250131003603.png]]
```python
extends CharacterBody3D

const SPEED = 5.0
const JUMP_VELOCITY = 4.5

func _physics_process(delta: float) -> void:
	# Add the gravity.
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Handle jump.
	if Input.is_action_just_pressed("ui_accept") and is_on_floor():
		velocity.y = JUMP_VELOCITY

	# Get the input direction and handle the movement/deceleration.
	# As good practice, you should replace UI actions with custom gameplay actions.
	var input_dir := Input.get_vector("ui_left", "ui_right", "ui_up", "ui_down")
	var direction := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	if direction:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		velocity.z = move_toward(velocity.z, 0, SPEED)

	move_and_slide()
```

### Area3D

Area3Dを追加しても物体は通り抜ける。
あくまでエリアに入ったかどうかを判定するためのノードだから。

![[Pasted image 20250131004019.png]]

---

## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その６　CharacterBody3Dを動かしましょう
https://qiita.com/FootInGlow/items/8287ebe80d1eeb5aebb8

Unityよりも優しい。お膳立てしてある感じ。

### move_and_slide

```python
extends CharacterBody3D

func _physics_process(delta):
	velocity = Vector3(0.0, 0.0, -1.0).normalized()
	move_and_slide()
	print(velocity)
```

- `extends CharacterBody3D`  
    　CharacterBody3Dを継承。
- `func _physics_process(delta):` 
    　`_process`ではなく、`_physics_process`メソッドを使用。  
    　物理エンジンの演算結果（現在位置、向き、速度など）を使用するまたは、動きの指示をする場合は、`_physics_process`メソッドの中で実行。
- `velocity = Vector3(0.0, 0.0, -1.0).normalized()`  
    　速度を設定。単位はm/s。
- `move_and_slide()`  
    　設定した速度でノードを動かすように物理エンジンに依頼。  
    　衝突などの速度に変化があった場合、velocityが上書きされる。

### move_and_collide

衝突したときの動きを実装したい場合、move_and_collideを使用。

```python
extends CharacterBody3D

func _ready():
	velocity = Vector3(0.0, 0.0, -1.0) 

func _physics_process(delta):
	var collision_info = move_and_collide(velocity.normalized() * delta)
	if collision_info:
		velocity = velocity.bounce(collision_info.get_normal())

```

- `move_and_collide(m_velocity.normalized() * delta)`  
	　`move_and_collide`の場合は、引数に経過時間(delta)あたりの移動量を設定。
- `var collision_info = move_and_collide(velocity.normalized() * delta)`  
    　衝突情報として`KinematicCollision3D`データが返却される。
- `if collision_info:`  
    　衝突した場合`collision_info`には何か設定されるので`true`になる。
- `velocity = velocity.bounce(collision_info.get_normal())`  
    　`collision_info.get_normal()`で衝突面（の法線）を取得。`normalized`（正規化）とは別物。  
    　`velocity.bounce`で衝突面に反発するような速度を取得。

---

## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その７　3D空間のタッチしたところに移動したい
https://qiita.com/FootInGlow/items/564dfe87dc7fc5135c67

タッチした座標は画面上の2次元の座標だが、ノードは3D空間に存在するため変換が必要。
２点間に光線を飛ばして、床との衝突を検知することで床の座標を取得する。

### マウスでタッチ操作をエミュレートする
プロジェクト設定　＞　入力デバイス　＞　ポインティングマウス　＞　タッチ操作をエミュレートする　をONに。

![[Pasted image 20250201235215.png]]

CharacterBody3Dのノードのスクリプトに以下を追加。

```python
extends CharacterBody3D

func _input(event):
	print(event)
```

マウスを移動すると出力ウィンドウにログが流れる。
```python
InputEventMouseMotion: button_mask=0, position=((576, 532)), relative=((18, 8)), velocity=((506.3439, 311.5963)), pressure=0.00, tilt=((0, 0)), pen_inverted=(false)
InputEventMouseMotion: button_mask=0, position=((584, 537)), relative=((8, 6)), velocity=((506.3439, 311.5963)), pressure=0.00, tilt=((0, 0)), pen_inverted=(false)
InputEventMouseMotion: button_mask=0, position=((585, 538)), relative=((0, 0)), velocity=((506.3439, 311.5963)), pressure=0.00, tilt=((0, 0)), pen_inverted=(false)
InputEventKey: keycode=4194327 (Command), mods=none, physical=false, location=left, pressed=false, echo=false
```

### タッチ位置を保持する

```python
var m_touch_pos = Vector2.ZERO

func _input(event):
	if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):
		m_touch_pos = event.position
		print("m_touch_pos:", m_touch_pos)
```

- `var m_touch_pos = Vector2.ZERO`  
    　タッチ位置を保存するメンバ変数
- `if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):`  
    　タッチスクリーンデバイスを使用している場合、タッチは`InputEventScreenTouch`、タッチドラッグは`InputEventScreenDrag`で通知される
- `m_touch_pos = event.position`  
    　タッチした位置を保持する

### タッチしたノードを検出する

```python
extends CharacterBody3D

@export var m_ray_length_m = 1000		# [m]十分な長さにする
var m_touch_pos = Vector2.ZERO

func _input(event):
	if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):
		m_touch_pos = event.position

func _physics_process(delta):
	var camera = get_viewport().get_camera_3d()

	# カメラを利用して3D空間のカメラ位置とタッチしたピクセルに対応する方向の1000m先の位置を計算する
	var from3d = camera.project_ray_origin(m_touch_pos)
	var to3d = from3d + camera.project_ray_normal(m_touch_pos) * m_ray_length_m

	# 3D ray physics queryの作成
	var query = PhysicsRayQueryParameters3D.create(from3d, to3d)
	query.collide_with_areas = true		# Area3Dを検知できるようにする
	
	# Godotの3Dの物理とコリジョンを保存しているspaceという情報を使用してオブジェクト検出
	var space_state = get_world_3d().direct_space_state
	var result = space_state.intersect_ray(query)
	if result:
		print(result)
```

- `func _physics_process(delta):`  
    　3D物理空間情報は`_physics_process`メソッドの中で使用する。
- `var camera = get_viewport().get_camera_3d()`  
    　今表示しているカメラを取得する。
- `var from3d = camera.project_ray_origin(m_touch_pos)`  
    　タッチした位置からカメラの位置を取得する。  
    　`Camera3D/ProjectionがPerspective`の場合、`print`するとクリックしたピクセルによらず(`0.0m,7.0m,0.0m)`と表示されるため、`Camera3D`ノードの`Node3D/Transform/position`を取得する。
- `camera.project_ray_normal(m_touch_pos)`  
    　カメラは`from3d`の1点から画角の範囲を放射状に見ていて、`m_touch_pos`でクリックしたピクセルを見ている方向を`camera.project_ray_normal`で取得します。長さ1mの距離です。
- `var to3d = from3d + camera.project_ray_normal(m_touch_pos) * m_ray_length_m`  
    　カメラの位置（`from3d`）からカメラが見ている方向へ1000m先の点を取得する。
- `var query = PhysicsRayQueryParameters3D.create(from3d, to3d)`  
    　`query`のひな型を作成。
- `query.collide_with_areas = true`
    　デフォルトでは`Area3D`を検出しないが、`collide_with_areas = true`にすると検出できるようになる。
- `var space_state = get_world_3d().direct_space_state`  
    　3D物理空間情報（`PhysicsDirectSpaceState3D`クラス）を取得する。`_physics_process`メソッドの中で使用が推奨されている。
- `var result = space_state.intersect_ray(query)`  
    　`from3d`から`to3d`を見たときに一番最初に衝突（検出）した情報が返却される。
- `if result:`  
    　衝突したノードがある場合。

### タッチしたところに移動する

```python
extends CharacterBody3D

@export var m_speed_mps = 5.0
@export var m_ray_length_m = 1000		# [m]十分な長さにする
var m_touch_pos = Vector2.ZERO
var m_d_move_to_pos_x_m = 0

func _input(event):
	if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):
		m_touch_pos = event.position

func _physics_process(delta):
	var camera = get_viewport().get_camera_3d()

	# カメラを利用して3D空間のカメラ位置とタッチしたピクセルに対応する方向の1000m先の位置を計算する
	var from3d = camera.project_ray_origin(m_touch_pos)
	var to3d = from3d + camera.project_ray_normal(m_touch_pos) * m_ray_length_m

	# 3D ray physics queryの作成
	var query = PhysicsRayQueryParameters3D.create(from3d, to3d)
	query.collide_with_areas = true		# Area3Dを検知できるようにする
	
	# Godotの3Dの物理とコリジョンを保存しているspaceという情報を使用してオブジェクト検出
	var space_state = get_world_3d().direct_space_state
	var result = space_state.intersect_ray(query)
	if result:
		m_d_move_to_pos_x_m = result.position.x

	# 移動制御は毎周期実行する
	var d_diff_x_m = (m_d_move_to_pos_x_m - transform.origin.x);
	var d_speed_mps = m_speed_mps if d_diff_x_m>0 else -m_speed_mps
	var d_move_diff_x_m = d_speed_mps * delta
	if absf(d_move_diff_x_m) > absf(d_diff_x_m):
		# 移動すると目標位置を超えるため、目標位置を設定する
		transform.origin.x = m_d_move_to_pos_x_m
	else:
		move_and_collide(Vector3(d_move_diff_x_m, 0, 0))
```

- `m_d_move_to_pos_x_m = result.position.x`  
    　`result`が設定されている（タッチ検出した）場合、検出位置のx座標を保存する。
- `var d_diff_x_m = (m_d_move_to_pos_x_m - transform.origin.x);`  
    　目標位置と現在のX座標の差分を計算する。
- `var d_speed_mps = m_speed_mps if d_diff_x_m>0 else -m_speed_mps`  
    　`d_diff_x_m`がプラスの場合は目標位置がX座標プラス側（画面右側）にあるため、プラスの速度で保存する。  
    　そうではない（０以上の場合）は目標位置と同一もしくはX座標マイナス側（画面左側）にあるため、マイナスの速度で保存する。
- `var d_move_diff_x_m = d_speed_mps * delta`  
    　速度に`delta`をかけて、前回から今回の時間分の移動距離を計算する。
- `if absf(d_move_diff_x_m) > absf(d_diff_x_m):`  
    　`abs`で絶対値をとり、移動距離(`d_move_diff_x_m`)分移動すると目標位置を超えるか判定する。
- `transform.origin.x = m_d_move_to_pos_x_m`  
    　目標位置を超えると判定した場合、目標位置に到達したというなので、目標位置を代入する。
- `else:`
- `move_and_collide(Vector3(d_move_diff_x_m, 0, 0))`  
    　目標位置まで十分な距離がある場合、移動指示をする。

マウスをクリックすると、タッチ位置まで移動する。（X軸だけ）

![[Pasted image 20250202003602.png]]

---

## 【Godot 4.0】スマホ3Dゲームを作るための勉強　その８　弾を発射する
https://qiita.com/FootInGlow/items/dd0f63bf7a9cd92801b7

### 新規シーンを作成する
シーンメニュー > 新規シーン > その他のノードボタン > CharacterBody3Dノードを追加。
CharacterBody3Dノードの名前を「Bullet」に変更。
![[Pasted image 20250203012758.png]]
保存するとシーンデータが保存される。
![[Pasted image 20250203012830.png]]
形状をCapsuleに変更。
Radiusを0.1m、Heightを0.5mに設定。
![[Pasted image 20250203013057.png]]
スクリプトを追加。
![[Pasted image 20250203013306.png]]
```python
extends CharacterBody3D

@export var m_v_dir = Vector3(0.0, 0.0, -1.0)
@export var m_d_speed_mps = 2.0

func _physics_process(delta):
	velocity = m_v_dir.normalized() * m_d_speed_mps
	move_and_slide()
```

### 一次的にシーンの動きを確認する
GlobalシーンにBulletシーンを追加。

![[Pasted image 20250203013420.png]]
実行すると弾が前に進んでゆく。
![[Pasted image 20250203013605.png]]
確認したのでGlobalシーンからBulletシーンを削除。
（右クリックメニューが縦に長くて見切れているが、下にスクロールすると「削除」がある）

### Timerノードを追加して周期的にsignalを受信する

Godotにはノード間で通信をするためのsignal通信の仕組みがある。
Timerノードは指定した時間経過すると、timeoutシグナルを発行することができ、同じシーン内の任意のスクリプトで受信することができる。タイマーは自動で繰り返される。1回だけタイマーを起動することも簡単にできるため、時間でイベントが欲しいときにはとても便利。

![[Pasted image 20250204015037.png]]

Playerノードを右クリックして「子ノードを追加」からTimerを追加。
Timerをクリック選択した状態でインスペクタを表示し、
Autostartをオンにする。実行すると自動的にタイマーが開始する。

![[Pasted image 20250204015232.png]]

インスペクタの隣の「ノード」タブをクリック。
timeoutしたときにPlayerノードに通知してほしいため、timeoutを右クリックして「接続」する。

![[Pasted image 20250204015440.png]]
同じシーンの中のスクリプトを持つノードを選択できる。受信側メソッド名を確認して、Playerを選択して接続ボタンを押下する。
Playerのスクリプトを見ると、先ほどのダイアログに表示されていた受信側メソッドが追加される。

![[Pasted image 20250204015716.png]]

関数の左側にある「→コ」みたいなアイコンをクリックすると、送信元であるソースノード、送信先のターゲットノードとその間を接続するシグナル名を表示できる。

### timeout時にBulletシーンを追加する

```python
extends CharacterBody3D

@export var m_speed_mps = 5.0
@export var m_ray_length_m = 1000
@export var m_scn_bullet : PackedScene

var m_touch_pos = Vector2.ZERO
var m_d_move_to_pos_x_m = 0
var m_f_is_screen_touch = false

func _input(event):
	if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):
		m_touch_pos = event.position
		# スマホタッチ中フラグ
		m_f_is_screen_touch = event.is_pressed() or ( event is InputEventScreenDrag )

func _physics_process(delta: float) -> void:
	var camera = get_viewport().get_camera_3d()

	# カメラを利用して3D空間のカメラ位置とタッチしたピクセルに対応する方向の1000m先の位置を計算する
	var from3d = camera.project_ray_origin(m_touch_pos)
	var to3d = from3d + camera.project_ray_normal(m_touch_pos) * m_ray_length_m

	# 3D ray physics queryの作成
	var query = PhysicsRayQueryParameters3D.create(from3d, to3d)
	query.collide_with_areas = true		# Area3Dを検知できるようにする
	
	# Godotの3Dの物理とコリジョンを保存しているspaceという情報を使用してオブジェクト検出
	var space_state = get_world_3d().direct_space_state
	var result = space_state.intersect_ray(query)
	if result:
		print(result)
		m_d_move_to_pos_x_m = result.position.x

	# 移動制御は毎周期実行する
	var d_diff_x_m = (m_d_move_to_pos_x_m - transform.origin.x);
	var d_speed_mps = m_speed_mps if d_diff_x_m>0 else -m_speed_mps
	var d_move_diff_x_m = d_speed_mps * delta
	if absf(d_move_diff_x_m) > absf(d_diff_x_m):
		# 移動すると目標位置を超えるため、目標位置を設定する
		transform.origin.x = m_d_move_to_pos_x_m
	else:
		move_and_collide(Vector3(d_move_diff_x_m, 0, 0))


func _on_timer_fire_timeout() -> void:
	# タッチ中の場合、発射する
	if m_f_is_screen_touch:
		var scn_bullet = m_scn_bullet.instantiate()
		scn_bullet.transform.origin = transform.origin
		add_sibling(scn_bullet)

```

- `var m_f_is_screen_touch = false`  
    スマホタッチ状態を保持できるようにメンバ変数を追加する。
- `m_f_is_screen_touch = event.is_pressed() or ( event is InputEventScreenDrag )`  
    　`_input()`の中で、`event`が`InputEventScreenDrag`且つ`event.is_pressed()`がtrueの時、もしくは`event`が`InputEventScreenDrag`のときに、タッチ中と判断する。
- `func _on_timer_fire_timeout():`  
    Timer_Fireのtimeoutシグナルを接続したときに追加したメソッド。
- `if m_f_is_screen_touch:`  
    タッチ中のみ以下の処理を実行。
- `var scn_bullet = m_scn_bullet.instantiate()`  
    新規にBulletシーンのインスタンスを生成する。  
    m_scn_bulletには先ほどドラッグアンドドロップでbullet.tscnをセットする。    
- `scn_bullet.transform.origin = transform.origin`  
    Playerと同じ位置を設定する。
- `add_sibling(scn_bullet)`  
    Playerと同じ階層にBulletシーンインスタンスを追加。 　  
    シーンを追加する場合、add_childとadd_siblingがある。  
    add_childではPlayerの子ノードとして追加される。その場合Playerが移動するとBulletもついてきて期待する動作とは異なる結果になる。  
    add_siblingとするとPlayerと同じ階層に追加される。

![[Pasted image 20250204020643.png]]

シーン内のPlayerをクリック選択してからインスペクタを見ると、M Scn Bulletという名前で表示されている。まだ何も登録していないため、`<empty>`となっている。
ファイルシステムのbullet.tscnを`<empty>`にドラッグアンドドロップして登録する。

![[Pasted image 20250204020746.png]]

こんな感じに弾を発射するようになった。

![[Pasted image 20250204021430.png]]

問題が２つある。ひとつはBulletが消えないこと。
２つ目の問題は、BulletやPlayerの球がおかしな方向（Z軸方向）に動くこと。
これは衝突判定をしているPlayerの中にBulletを生成したので、衝突による速度計算がおかしくなっているため。

### 画面外に出た場合Bulletを消すようにする

bullet.tscnを開いて、Bulletを右クリックして「子ノードを追加」から`VisibleOnScreenNotifier3D`を追加する。

![[Pasted image 20250204021815.png]]
screen_exitedを右クリックして、Bulletのスクリプトに接続する。
追加される`func _on_visible_on_screen_notifier_3d_screen_exited():`にてインスタンスを削除（開放）する。
- queue_free()  
    自身のノードインスタンスを開放（削除）する。

![[Pasted image 20250204022337.png]]

### Bulletの発射位置を指定する

Playerを右クリックして「子ノードを追加」からMarker3Dを追加。
![[Pasted image 20250204023522.png]]

Marker3Dの位置を利用してBulletの発射位置を設定。

```python
extends CharacterBody3D

@export var m_speed_mps = 5.0
@export var m_ray_length_m = 1000
@export var m_scn_bullet : PackedScene

var m_touch_pos = Vector2.ZERO
var m_d_move_to_pos_x_m = 0
var m_f_is_screen_touch = false

func _input(event):
	if ( event is InputEventScreenDrag ) or ( event is InputEventScreenTouch ):
		m_touch_pos = event.position
		# スマホタッチ中フラグ
		m_f_is_screen_touch = event.is_pressed() or ( event is InputEventScreenDrag )

func _physics_process(delta: float) -> void:
	var camera = get_viewport().get_camera_3d()

	# カメラを利用して3D空間のカメラ位置とタッチしたピクセルに対応する方向の1000m先の位置を計算する
	var from3d = camera.project_ray_origin(m_touch_pos)
	var to3d = from3d + camera.project_ray_normal(m_touch_pos) * m_ray_length_m

	# 3D ray physics queryの作成
	var query = PhysicsRayQueryParameters3D.create(from3d, to3d)
	query.collide_with_areas = true		# Area3Dを検知できるようにする
	
	# Godotの3Dの物理とコリジョンを保存しているspaceという情報を使用してオブジェクト検出
	var space_state = get_world_3d().direct_space_state
	var result = space_state.intersect_ray(query)
	if result:
		print(result)
		m_d_move_to_pos_x_m = result.position.x

	# 移動制御は毎周期実行する
	var d_diff_x_m = (m_d_move_to_pos_x_m - transform.origin.x);
	var d_speed_mps = m_speed_mps if d_diff_x_m>0 else -m_speed_mps
	var d_move_diff_x_m = d_speed_mps * delta
	if absf(d_move_diff_x_m) > absf(d_diff_x_m):
		# 移動すると目標位置を超えるため、目標位置を設定する
		transform.origin.x = m_d_move_to_pos_x_m
	else:
		move_and_collide(Vector3(d_move_diff_x_m, 0, 0))


func _on_timer_fire_timeout():
	# スマホタッチ中の場合、発射する
	if m_f_is_screen_touch:
		var scn_bullet = m_scn_bullet.instantiate()
		scn_bullet.transform.origin = $Marker3D.global_transform.origin
		add_sibling(scn_bullet)

```

- `scn_bullet.transform.origin = $Marker3D.global_transform.origin`
	Marker3Dのglobal座標をBulletに設定

無事に弾が発射されるようになった。



